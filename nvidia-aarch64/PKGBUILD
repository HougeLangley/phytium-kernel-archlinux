# Maintainer : Daniel Bermond <dbermond@archlinux.org>
# Contributor: Det <nimetonmaili g-mail>
# Contributor: Ng Oon-Ee
# Contributor: Dan Vratil

pkgname=nvidia-aarch64
pkgver=515.76
pkgrel=1
pkgdesc="NVIDIA drivers for Arch's official 'linux' package (beta version)"
arch=('aarch64')
url='https://www.nvidia.com/'
license=('custom')
makedepends=('linux-phytium-headers' 'dkms')
provides=("nvidia=${pkgver}" "nvidia-aarch64=${pkgver}" 'NVIDIA-MODULE')
conflicts=('nvidia')
options=('!strip')
_pkg="NVIDIA-Linux-${arch}-${pkgver}"
source=("http://us.download.nvidia.com/XFree86/aarch64/${pkgver}/${_pkg}.run"
        '110-nvidia-change-dkms-conf.patch')
sha256sums=('cbea88605164022ade03d74fb3ed28ef9cb4efe2af3cf904967032f890756cd3'
            '23a2118c80a3db13c696fbd543817faf6bfee6a5a5d50f61bebe99a3fd3116ec')

prepare() {
    # extract the source file
    [ -d "$_pkg" ] && rm -rf "$_pkg"
    printf '%s\n' "  -> Self-Extracting ${_pkg}.run..."
    sh "${_pkg}.run" --extract-only
    ln -s kernel "${_pkg}/nvidia-${pkgver}"
    
    patch -d "$_pkg" -Np1 -i "${srcdir}/110-nvidia-change-dkms-conf.patch"
}

build() {
    fakeroot dkms build --dkmstree "$srcdir" --sourcetree "${srcdir}/${_pkg}" -m "nvidia/${pkgver}" -k "$(</usr/src/linux-phytium/version)"
}

package() {
    depends=('linux' "nvidia-utils-aarch64=${pkgver}" 'libglvnd')
    
    local _kernver
    _kernver="$(</usr/src/linux-phytium/version)"
    
    install -D -m644 "nvidia/${pkgver}/${_kernver}/${CARCH}/module"/* -t "${pkgdir}/usr/lib/modules/${_kernver}/extramodules"
    install -D -m644 "${_pkg}/LICENSE" -t "${pkgdir}/usr/share/licenses/${pkgname}"
    find "$pkgdir" -name '*.ko' -exec xz -T1 {} +
}
